  servicehealthresources
  | where type == "microsoft.resourcehealth/events" 
  | extend eventType = tostring(properties.EventType)
  | where eventType in ('HealthAdvisory', 'SecurityAdvisory')
  | extend lastUpdate =  todatetime(tolong(properties.LastUpdateTime))
  | where lastUpdate > ago(28d)
  // try to replace HTML tags in the Summary by empty string
  | extend temp1 = replace('\\<[\\w/]+\\>', '', tostring(properties.Summary))
  | extend summary = replace('\\<[\\w/]+\\>', '', temp1)
  | extend service = tostring(properties.Impact[0].ImpactedService)
  | extend title = tostring(properties.Title)
  | extend url = strcat('https://app.azure.com/h/', name)
  | summarize arg_max (lastUpdate, *) by name
  | project lastUpdate, eventType, name, service, ['title'], summary, url
  // most recent update at the bottom, so we can paste new records to the end of an XLS
  | sort by lastUpdate asc